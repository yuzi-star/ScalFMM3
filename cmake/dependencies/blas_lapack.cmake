#
# Blas options
# ------------
if(${CMAKE_PROJECT_NAME}_USE_MKL)
    set(MKL_ARCH "intel64")
    if(UNIX)
        set(MKL_LINK "static")
    elseif(MSVC)
        set(MKL_LINK "dynamic")
    endif()
    set(MKL_THREADING "sequential")
    set(MKL_INTERFACE "lp64")
    find_package(MKL CONFIG REQUIRED)
    list(APPEND FUSE_LIST CBLAS)
    set(CBLAS_FOUND TRUE)
    set_target_properties(MKL::MKL PROPERTIES
        INTERFACE_INCLUDE_DIRECTORIES
        "${MKL_ROOT}/include;${MKL_ROOT}/include/fftw"
    )
    list(APPEND CBLAS_TARGET MKL::MKL)
else()
    cmake_print_variables(BLA_VENDOR)
    if(NOT BLA_VENDOR)
        message(WARNING "You are not using Intel MKL, please provide a blas vendor through the BLA_VENDOR variable !")
    endif()
    find_package(BLAS REQUIRED)
    if(DEFINED BLAS_FOUND)
        list(APPEND CBLAS_TARGET BLAS::BLAS)
        list(APPEND FUSE_LIST CBLAS)
        set(CBLAS_FOUND TRUE)
        find_package(LAPACK REQUIRED)
        if(DEFINED LAPACK_FOUND)
            list(APPEND CBLAS_TARGET LAPACK::LAPACK)
        else()
            message(FATAL_ERROR "LAPACK required for uniform/Chebyshev m2l handler.")
        endif()
    else()
        message(FATAL_ERROR "CBLAS and BLAS required for uniform/Chebyshev interpolator.")
    endif()
endif()

